model ModelName
uses "mmxprs";
setparam("REALFMT", "%.3f")

declarations
	numCities = 5
    numDays = 10
    
	CITIES = 1..numCities
    DAYS = 1..numDays
	
	location : array(CITIES, DAYS) of mpvar
	city : array(CITIES) of mpvar
	move : array(CITIES, CITIES, DAYS) of mpvar
	daysStay : array(CITIES, DAYS) of mpvar
	
	names: array(CITIES) of string 
	travel : array(CITIES, CITIES) of integer
	daily : array(CITIES) of integer
	
	baseUtility: array(CITIES) of real
	utility: array(CITIES, DAYS) of real
	
 	minDays : integer
 	maxDays : integer
 	
 	cityDays: array(CITIES) of linctr
 	totalUtility: linctr
 	totalCost : linctr
end-declarations

function findUtility(city:integer, day:integer):real
	!returned := baseUtility(city) * (-0.2*day^2 + 0.8 * day + 0.4) !Alternative utility function
	returned := baseUtility(city) * 0.9^day
end-function

names:: (1..5)["Moscow","Paris","London","Madrid","Rome"]

baseUtility :: [1.209, 4.483, 4.878, 2.529, 4.2]

forall(c in CITIES) do
	utility(c,1) := baseUtility(c)
end-do

forall(c in CITIES, d in 2..numDays) do
	utility(c,d) := utility(c,d-1) + findUtility(c,d)
end-do

travel :: [0, 146, 126, 202, 146,
		   227, 0, 60, 143, 93,
		   213, 82, 0, 249, 160,
		   188, 86, 136, 0, 144,
		   223, 80, 125, 146, 0]

daily :: [10, 10, 20, 30, 5]

forall(c in CITIES) do
    sum (d in DAYS) daysStay(c, d) = city(c)
end-do

forall(d in 1..(numDays-1), f in CITIES, t in CITIES) do
    location(f, d) + location(t, d+1) <= 1 + move(f, t, d)
end-do

forall(d in DAYS) do
    sum (c in CITIES) location(c, d) = 1
    sum (f in CITIES, t in CITIES) move(f, t, d) = 1
end-do

forall(c in CITIES, d in DAYS) do
    city(c) is_binary
    location(c, d) is_binary
    daysStay(c, d) is_binary
end-do

forall(f in CITIES, t in CITIES, d in DAYS) do
    move(f, t, d) is_binary
end-do

forall(c in CITIES, d in DAYS) do
	daysStay(c, d) * d <= sum(d2 in DAYS) location(c, d2)
end-do

totalCost := sum(c in CITIES, d in DAYS) location(c, d) * daily(c) +
             sum(f in CITIES, t in CITIES, d in DAYS) move(f, t, d) * travel(f, t)

totalUtility := sum(c in CITIES, d in DAYS) daysStay(c, d) * utility(c, d)

maximise(totalUtility)

totalUtility = totalUtility.sol

minimise(totalCost)

writeln("Begin running model")

writeln("Total Cost: $", totalCost.sol)

forall(d in DAYS, c in CITIES) do
    if (location(c, d).sol > 0) then
        writeln("Day ", d, " in City ", names(c))
    end-if
end-do

writeln("Total utility is ", totalUtility.sol)
forall(c in CITIES) do
	write(baseUtility(c),"\t")
end-do
writeln
forall(c in CITIES) do
	forall(d in DAYS) do
		write(utility(c,d), "\t")
	end-do
	write("\n")
end-do
writeln("End running model")

end-model