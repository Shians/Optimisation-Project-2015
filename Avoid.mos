model ModelName
uses "mmxprs";

declarations
    numCities = 15
    numDays = 10
    numPeople = 2

    CITIES = 1..numCities
    DAYS = 1..numDays
    PEOPLE = 1..numPeople

    location : array(CITIES, DAYS, PEOPLE) of mpvar
    city : array(CITIES, PEOPLE) of mpvar
    move : array(CITIES, CITIES, 1..(numDays-1), PEOPLE) of mpvar
    
    together : array(CITIES, DAYS) of mpvar
    daysTogether : linctr
    
    partner : array(CITIES, DAYS) of real
    budget : array(PEOPLE) of integer

    names: array(CITIES) of string  
    travel : array(CITIES, CITIES) of integer
    daily : array(CITIES) of integer

    minDays : integer
    maxDays : integer

    totalCost : array(PEOPLE) of linctr
end-declarations

budget(1) := 1000
budget(2) := 500

names :: (1..15)["Moscow","Paris","London","Madrid","Rome","Crete","Barcelona","Berlin","Budapest","Florence","Amsterdam","Prague","Istanbul","Vienna","Venice"]

travel :: [0,146,126,202,146,168,143,156,207,291,154,146,115,131,129,
           227,0,60,143,93,123,139,110,162,85,52,165,150,140,68,
           213,82,0,249,160,284,135,107,163,224,124,171,197,179,152,
           188,86,136,0,144,188,69,70,96,138,103,124,180,143,119,
           223,80,125,146,0,177,39,96,69,84,94,94,74,105,71,
           233,81,223,188,58,0,131,123,58,241,200,114,120,164,169,
           200,121,79,76,32,165,0,143,108,103,90,110,141,103,74,
           130,53,81,97,83,153,110,0,83,214,92,123,99,176,104,
           249,133,125,96,41,123,96,76,0,164,157,113,50,149,97,
           309,246,162,172,86,301,90,186,204,0,227,155,279,229,150,
           155,158,73,141,136,211,117,106,125,229,0,85,121,155,95,
           112,122,100,112,48,193,116,108,138,144,97,0,85,152,65,
           123,101,127,154,67,173,122,86,41,235,66,108,0,59,65,
           145,162,122,202,90,75,145,117,175,171,90,181,121,0,194,
           167,65,17,117,84,191,97,119,110,152,75,96,177,152,0]

daily :: [10, 10, 20, 30, 5, 10, 15, 20, 35, 5, 10, 15, 25, 5, 5]

minDays := 2
maxDays := 4

forall(c in CITIES, p in PEOPLE) do
    sum (d in DAYS) location(c, d, p) >= minDays * city(c, p)
    sum (d in DAYS) location(c, d, p) <= maxDays * city(c, p)
end-do

forall(d in 1..(numDays-1), f in CITIES, t in CITIES, p in PEOPLE) do
    location(f, d, p) + location(t, d+1, p) <= 1 + move(f, t, d, p)
end-do

forall(d in DAYS, p in PEOPLE) do
    sum (c in CITIES) location(c, d, p) = 1
end-do

forall(c in CITIES, d in DAYS, p in PEOPLE) do
    city(c, p) is_binary
    location(c, d, p) is_binary
    together(c, d) is_binary
end-do

forall(d in 1..(numDays - 1), p in PEOPLE) do
	sum (f in CITIES, t in CITIES) move(f, t, d, p) = 1
end-do

forall(f in CITIES, t in CITIES, d in 1..(numDays - 1), p in PEOPLE) do
    move(f, t, d, p) is_binary
end-do

forall(p in PEOPLE) do
	totalCost(p) := sum(c in CITIES, d in DAYS) location(c, d, p) * daily(c) +
    	         	sum(f in CITIES, t in CITIES, d in 1..(numDays-1)) move(f, t, d, p) * travel(f, t)
end-do

forall(p in PEOPLE) do
	totalCost(p) <= budget(p)
end-do

minimise(totalCost(1))

writeln("Begin running model for Person 1")

writeln("Total Cost: ", totalCost(1).sol)

forall(d in DAYS, c in CITIES) do
    if (location(c, d, 1).sol > 0) then
        writeln("Day ", d, " in ", names(c), " ", c)
    end-if
end-do

writeln("End running model for Person 1")

forall(c in CITIES, d in DAYS) do
	partner(c, d) := location(c, d, 1).sol
end-do

forall(c in CITIES, d in DAYS) do
	location(c, d, 2) + partner(c, d) <= 1 + together(c, d)
end-do

daysTogether := sum(c in CITIES, d in DAYS) together(c, d)

minimise(daysTogether)

sum(c in CITIES, d in DAYS) together(c, d) = daysTogether.sol

minimise(totalCost(2))

writeln("Begin running model for Person 2")

writeln("Together: ", daysTogether.sol, " days")

writeln("Total Cost: ", totalCost(2).sol)

forall(d in DAYS, c in CITIES) do
    if (location(c, d, 2).sol > 0) then
        writeln("Day ", d, " in ", names(c), " ", c)
    end-if
end-do

writeln("End running model for Person 2")

end-model